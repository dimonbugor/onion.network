apply plugin: 'com.android.application'

android {
    namespace 'onion.network'
    compileSdkVersion 36

    defaultConfig {
        applicationId "onion.network"
        targetSdkVersion 36
        versionCode 1
        versionName "1.0"
        minSdkVersion 24
        multiDexEnabled = true

        externalNativeBuild {
            cmake {
                cppFlags ""
            }
        }

        ndk {
            abiFilters "armeabi-v7a", "arm64-v8a"
        }
        signingConfig signingConfigs.debug
    }

    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
        }
    }

    buildTypes {

        release {
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            debuggable true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['src/main/jniLibs', 'libs']
        }
    }

    packagingOptions {
        resources {
            exclude 'META-INF/androidx.interpolator_interpolator.version'
            exclude 'META-INF/androidx.customview_customview.version'
            exclude 'META-INF/androidx.drawerlayout_drawerlayout.version'
            excludes += ['META-INF/androidx.localbroadcastmanager_localbroadcastmanager.version', 'META-INF/DEPENDENCIES', 'META-INF/LICENSE', 'META-INF/LICENSE.txt', 'META-INF/license.txt', 'META-INF/NOTICE', 'META-INF/NOTICE.txt', 'META-INF/notice.txt', 'META-INF/ASL2.0', 'META-INF/*.kotlin_module', 'info/pluggabletransports/dispatch/Connection.class']
        }
        jniLibs {
            useLegacyPackaging true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    dependenciesInfo {
        includeInApk true
        includeInBundle true
    }
    buildFeatures {
        viewBinding true
        buildConfig true
    }

}

dependencies {
    implementation 'androidx.multidex:multidex:2.0.1'
    // https://mvnrepository.com/artifact/org.jsoup/jsoup
    implementation group: 'org.jsoup', name: 'jsoup', version: '1.21.2'

    implementation "androidx.media3:media3-exoplayer:1.4.1"
    implementation "androidx.media3:media3-ui:1.4.1"

    implementation 'com.google.android.play:integrity:1.5.0'

    implementation 'org.nanohttpd:nanohttpd:2.3.1'
    implementation 'com.squareup.okhttp3:okhttp:5.2.1'

    implementation 'org.bouncycastle:bcprov-jdk15on:1.70'
    implementation 'io.github.webrtc-sdk:android:137.7151.04'

    // Pick only the JARs we actually need to avoid classpath clashes with bcprov
    testImplementation 'junit:junit:4.13.2'
    implementation 'com.google.android.material:material:1.13.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.4.0'
    implementation 'com.google.zxing:core:3.5.3'
    implementation('org.apache.commons:commons-compress:1.28.0') {
        exclude group: 'commons-codec', module: 'commons-codec'
    }
    implementation 'org.tukaani:xz:1.10'
    implementation files('libs/commons-codec-1.10.jar')
    implementation files('libs/core-1.58.0.0.jar')
    implementation files('libs/prov-1.58.0.0.jar')
}

// Fix 16KB page size alignment for all native libs (16KB kernel page size devices)
afterEvaluate {
    def scriptPath = "${project.rootDir}/fix-alignment.py"
    def abis = ['arm64-v8a', 'armeabi-v7a']

    def alignElf = { File libFile ->
        if (!libFile.exists()) return
        def tmpFile = File.createTempFile(libFile.name, ".tmp")
        println "ðŸ”§ Aligning 16KB pages: ${libFile.path}"
        def result = exec {
            commandLine 'python3', scriptPath, libFile.path, tmpFile.absolutePath
            ignoreExitValue = true
        }
        if (result.exitValue == 0 && tmpFile.length() > 0) {
            exec {
                commandLine 'cp', tmpFile.absolutePath, libFile.path
                ignoreExitValue = true
            }
        }
        tmpFile.delete()
    }

    // Align prebuilt jniLibs before they are merged into the APK
    tasks.matching { it.name.startsWith('merge') && it.name.endsWith('JniLibFolders') }.all { mergeTask ->
        mergeTask.doFirst {
            abis.each { abi ->
                fileTree("${project.projectDir}/src/main/jniLibs/${abi}").matching {
                    include "*.so"
                }.each { libFile ->
                    alignElf(libFile)
                }
            }
        }
    }

    // Align native build outputs (CMake) after compilation
    tasks.matching { it.name.toLowerCase().contains('externalnativebuild') }.all { buildTask ->
        buildTask.doLast {
            abis.each { abi ->
                fileTree("${project.buildDir}/intermediates/cxx").matching {
                    include "**/obj/${abi}/*.so"
                }.each { libFile ->
                    alignElf(libFile)
                }
            }
        }
    }
}
